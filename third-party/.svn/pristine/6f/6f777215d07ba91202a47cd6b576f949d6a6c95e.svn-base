<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:esri="http://www.esri.com/2008/ags"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:samples="com.esri.ags.samples.*"
               pageTitle="InfoWindow as MapTips">
    <!--
         @@includeFiles com/esri/ags/samples/InfoWindowRollOverContent.mxml
    -->

    <s:layout>
        <s:VerticalLayout paddingBottom="20"
                          paddingLeft="20"
                          paddingRight="20"
                          paddingTop="20"/>
    </s:layout>

    <fx:Script>
        <![CDATA[
            import com.esri.ags.Graphic;
            import com.esri.ags.events.GraphicEvent;
            import com.esri.ags.geometry.MapPoint;

            // Constants to define the format.
            public static const LAT:String = "lat";
            public static const LON:String = "lon";

            /**
             * Utility function to format a decimal degree number into a pretty string with degrees, minutes and seconds.
             * @param decDeg the decimal degree number.
             * @param decDir "lat" for a latitude number, "lon" for a longitude value.
             * @return A pretty print string with degrees, minutes and seconds.
             */
            public static function format(
                decDeg:Number,
                decDir:String
                ):String
            {
                var d:Number = Math.abs(decDeg);
                const deg:Number = Math.floor(d);
                d = d - deg;
                var min:Number = Math.floor(d * 60);
                const av:Number = d - min / 60;
                var sec:Number = Math.floor(av * 60 * 60);
                if (sec == 60)
                {
                    min++;
                    sec = 0;
                }
                if (min == 60)
                {
                    deg++;
                    min = 0;
                }
                const smin:String = min < 10 ? "0" + min + "' " : min + "' ";
                const ssec:String = sec < 10 ? "0" + sec + "\"" : sec + "\"";
                const sdir:String = (decDir == LAT) ? (decDeg < 0 ? "S " : "N ") : (decDeg < 0 ? "W " : "E ");
                return sdir + deg + "\xB0 " + smin + ssec;
            }

            private function graphicAddHandler(event:GraphicEvent):void
            {
                var graphic:Graphic = event.graphic;
                graphic.addEventListener(MouseEvent.ROLL_OVER, rollOverHandler);
                graphic.addEventListener(MouseEvent.ROLL_OUT, rollOutHandler);
            }

            private function rollOverHandler(event:MouseEvent):void
            {
                const graphic:Graphic = event.target as Graphic;
                if (graphic)
                {
                    const mapPoint:MapPoint = MapPoint(graphic.geometry);
                    content.lat = mapPoint.y.toFixed(3);
                    content.lon = mapPoint.x.toFixed(3);
                    const point:Point = map.toScreen(mapPoint);
                    point.y -= mySimpleMarkerSymbol.size / 2;
                    map.infoWindow.show(map.toMap(point));
                }
            }

            private function rollOutHandler(event:MouseEvent):void
            {
                const graphic:Graphic = event.target as Graphic;
                if (graphic)
                {
                    map.infoWindow.hide();
                }
            }
        ]]>
    </fx:Script>

    <s:initialize>
        <![CDATA[
            map.infoWindow.labelVisible = false;
            map.infoWindow.closeButtonVisible = false;
        ]]>
    </s:initialize>

    <s:Label width="100%"
             fontSize="12"
             fontWeight="bold"
             text="Move your mouse over the red symbol to see an InfoWindow."/>
    <esri:Map id="map" openHandCursorVisible="false">

        <esri:extent>
            <esri:Extent xmin="-14279700" ymin="2402000" xmax="-7011000" ymax="6716700">
                <esri:SpatialReference wkid="102100"/>
            </esri:Extent>
        </esri:extent>
        <esri:infoWindowContent>
            <samples:InfoWindowRollOverContent id="content"/>
        </esri:infoWindowContent>
        <esri:ArcGISTiledMapServiceLayer url="http://server.arcgisonline.com/ArcGIS/rest/services/USA_Topo_Maps/MapServer"/>
        <esri:FeatureLayer id="citiesFeatureLayer"
                           definitionExpression="POP1990 &gt; 100000 AND STATE_NAME NOT IN ('Alaska','Hawaii')"
                           graphicAdd="graphicAddHandler(event)"
                           mode="snapshot"
                           outFields="[CITY_NAME,POP1990]"
                           url="http://sampleserver1.arcgisonline.com/ArcGIS/rest/services/Specialty/ESRI_StatesCitiesRivers_USA/MapServer/0">
            <esri:symbol>
                <esri:SimpleMarkerSymbol id="mySimpleMarkerSymbol"
                                         color="0xFF0000"
                                         size="10"
                                         style="diamond"/>
            </esri:symbol>
        </esri:FeatureLayer>
    </esri:Map>
</s:Application>
