<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:esri="http://www.esri.com/2008/ags"
        title="A tiled map service">

    <fx:Script>
        <![CDATA[
            import com.esri.ags.Graphic;
            import com.esri.ags.events.ExtentEvent;
            import com.esri.ags.events.MapEvent;
            import com.esri.ags.geometry.MapPoint;
            import com.esri.ags.symbols.PictureMarkerSymbol;
            import com.esri.ags.utils.WebMercatorUtil;

            import flash.sensors.Geolocation;

            [Embed(source="/assets/gpsA.png")]
            private var m_gpsA:Class;
            [Embed(source="/assets/gpsB.png")]
            private var m_gpsB:Class;

            private var geo:Geolocation;
            private var currentLocation:MapPoint;

            protected function myMap_loadHandler(event:MapEvent):void
            {
                if (Geolocation.isSupported)
                {
                    geo = new Geolocation;
                    if (!geo.muted) // check if Geolocation is not disabled on the device
                    {
                        zoomToCurrentLocationImg.source = m_gpsA;
                        zoomToCurrentLocationImg.visible = true;
                    }
                }
            }

            protected function zoomToCurrentLocation_clickHandler(event:MouseEvent):void
            {
                if (myGraphicsLayer.numGraphics > 0)
                {
                    myGraphicsLayer.clear();
                    zoomToCurrentLocationImg.source = m_gpsA;
                }
                else
                {
                    zoomToCurrentLocationImg.source = m_gpsB;
                    geo.addEventListener(GeolocationEvent.UPDATE, onUpdate);
                }
            }

            private function onUpdate(event:GeolocationEvent):void
            {
                geo.removeEventListener(GeolocationEvent.UPDATE, onUpdate);

                // convert the longitude and lagitude values to web mercator as the basemap is in web mercator
                currentLocation = WebMercatorUtil.geographicToWebMercator(new MapPoint(event.longitude, event.latitude)) as MapPoint;
                myMap.centerAt(currentLocation);
                myMap.level = 16;

                var graphic:Graphic = new Graphic(currentLocation);
                myGraphicsLayer.add(graphic);
            }
        ]]>
    </fx:Script>

    <fx:Declarations>
        <s:Fade id="fade"
                alphaFrom="1"
                alphaTo="0"
                duration="1500"/>
    </fx:Declarations>

    <!-- Map Control -->
    <esri:Map id="myMap"
              level="2"
              load="myMap_loadHandler(event)"
              wrapAround180="true"
              zoomSliderVisible="false">
        <esri:extent>
            <esri:Extent xmin="-19325128.942" ymin="-10948057.675" xmax="-539964.871" ymax="18364625.427">
                <esri:SpatialReference wkid="102100"/>
            </esri:Extent>
        </esri:extent>
        <esri:ArcGISTiledMapServiceLayer url="http://server.arcgisonline.com/ArcGIS/rest/services/World_Street_Map/MapServer"/>
        <esri:GraphicsLayer id="myGraphicsLayer" symbol="{new PictureMarkerSymbol}"/>
    </esri:Map>
    <s:Image id="zoomToCurrentLocationImg"
             bottom="0"
             click="zoomToCurrentLocation_clickHandler(event)"
             horizontalCenter="0"
             visible="false"/>
</s:View>
